group 'org.webscene'
version '0.1-SNAPSHOT'

buildscript {
    ext {
        KOTLIN_VER = '1.1.2-2'
        WEBSCENE_CLIENT_VER = '0.1-SNAPSHOT'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VER"
    }
}

apply plugin: 'kotlin2js'

def programName = 'notifier'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url uri('libs')
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$KOTLIN_VER"
    compile "org.webscene:webscene-client:$WEBSCENE_CLIENT_VER"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/web/js/${programName}.js"
}

task transpileToJs(type: Copy) {
    dependsOn 'build'
    dependsOn 'compileKotlin2Js'

    def src = "${projectDir.absolutePath}/assets/web"
    def dest = "${projectDir.absolutePath}/web"

    from src
    into dest
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/web/js"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
}